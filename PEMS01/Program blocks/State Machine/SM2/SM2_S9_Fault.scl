FUNCTION "SM2_S9_Fault" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1

BEGIN
	// **********************************************************
	// *                   MANAGEMENT ACTION                    *
	// **********************************************************
	
	REGION State Machine Status
	    
	    "SM2".xSsBootSt      := False;
	    "SM2".xSsWarmUpSt    := False;
	    "SM2".xSsReadySt     := False;
	    "SM2".xSsStandBySt   := False;
	    "SM2".xSsRunSt       := False;
	    "SM2".xSsTestSt      := False;
	    "SM2".xSsAutoMaintSt := False;
	    "SM2".xSsStopSt      := False;
	    "SM2".xSsFaultSt     := True;
	    
	    "SM2".iCountSS := 9;
	    
	END_REGION
	
	"SM2".xPCSrunCmd := False;      //In fault state send stop command to the PCS
	
	REGION PCS Enable Off
	    
	    IF "SM2".xPCSreadySt        //When the PCS are in ready take out the enable command to the PCS
	    THEN
	        "SM2".xPCSenbCmd := False;
	    END_IF;
	END_REGION
	
	REGION Command Open RMU and Battery
	    
	    IF "SM2".xPCSstopSt AND NOT "SM2".xVacPCS       //When we go in fault state have a possibility who the medium voltage is off
	    THEN
	        "SM2".xMVbreakerOpenCmd := True;        //If the medium voltage is off we need to open a MV breaker because otherwise after giving the open command to the battery,
	                                                //the PCS would switch off AND in this CASE you could NOT close the MV breaker anymore        
	        IF "SM2".xMVbreakerOpenSt       //When the MV breaker are opened semd a open command to the battery
	        THEN
	            "SM2".xBattCloseCmd := False;
	        END_IF;
	    END_IF;
	    
	END_REGION
	
	REGION Command Open Battery
	    
	    IF "SM2".xPCSstopSt AND "SM2".xVacPCS       //If the medium voltage is on and PCS stopped send a open command to the battery
	    THEN
	        "SM2".xBattCloseCmd := False;
	    END_IF;
	    
	END_REGION
	
	
	// **********************************************************
	// *                 MANAGEMENT TRANSITION                  *
	// **********************************************************
	
	// Transition from Alarm to WarmUp
	
	IF NOT "SM2".xFltSt AND "SM2".xBattOpenSt       //Without faults and battery open return in warmup state
	THEN
	    "SM2".xT91 := True;
	    "SM2".xMVbreakerOpenCmd := False;
	END_IF;
	
	// Reset Transition
	
	"SM2".xT22   := False;
	"SM2".xT33   := False;
	"SM2".xT44   := False;
	"SM2".xT53   := False;
	"SM2".xT62   := False;
	"SM2".xT73   := False;
	
END_FUNCTION

